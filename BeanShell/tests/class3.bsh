#!/bin/java bsh.Interpreter

source("TestHarness.bsh");

assert( Foo == void );

class Foo 
{
	static int a = 5;
	int b;
	int b2=42;
	bsh.This fooThis = this;
	q=5; // loose vars act like instance vars

	Foo( int c ) {
		b=c;
	}

	void method() { 
		assert( a == 5 );
		assert( q == 5 || q==6/*after setQ()*/);
		assert( this == fooThis );
		assert( this.namespace == fooThis.namespace );

		// block namespace
		{
			assert( this == fooThis );
			assert( this.namespace == fooThis.namespace );
		}
	}

	setQ(int a) { q=a; }

	static void smethod() 
	{
		assert( a == 5 );

		// can't see instance vars or methods from static
		assert(isEvalError("print(q)"));
		assert(isEvalError("print(b)"));
		assert(isEvalError("method()"));

		// static 'this' is method closure for now
		assert( this.namespace.getName().equals("smethod") );
	}

	method2() { 
		return "foo"; 
	}
}

class Bar extends Foo { 
	int b2=99; // override
	bsh.This barThis = this;

	barMethod() {
		assert( b2 == 99 );
		assert( this.b2 == 99 );
		assert( super.b2 == 42 );
		method();
		smethod();
		assert( this == barThis );
		assert( super == fooThis );
		assert( method2().equals("bar") );
		assert( super.method2().equals("foo") );
	}
	method2() { return "bar"; }
}

// static
assert(Bar.a == 5);
assert(isEvalError("print(Bar.b)"));
Bar.smethod();
assert(isEvalError("Bar.method()"));

// instance
bar = new Bar();
assert( bar.b == 0 ); // uninitialized instance
assert( bar.a == 5 ); // static
bar.method(); // instance
bar.smethod(); // static
assert( bar.q == 5 ); // loose instance
bar.setQ(6);
if ( Interpreter.LOCALSCOPING ) // not sure if we should allow this
	assert( bar.q == 5 );
else
	assert( bar.q == 6 );

// non default constructors
assert(isEvalError("new Bar(6)"));
bar2 = new Bar();
assert( bar2.b == 0 );
assert( bar2.a == 5 );
assert( bar2.q == 5 );
assert( bar2.b2 == 99 );

// Version 1.3 doesn't support this.
//assert( ((Foo)bar2).b2 == 42 );

// subclass tests
bar.barMethod();

class Gee extends Bar { }

// static
assert(Gee.a == 5);
assert(isEvalError("print(Gee.b)"));
Gee.smethod();
assert(isEvalError("Gee.method()"));

// instance
gee = new Gee();
assert( gee.b == 0 ); // uninitialized instance
assert( gee.a == 5 ); // static
gee.method(); // instance
gee.smethod(); // static
assert( gee.q == 5 ); // loose instance
gee.setQ(6);
if ( Interpreter.LOCALSCOPING ) // not sure if we should allow this
	assert( gee.q == 5 );
else
	assert( gee.q == 6 );

assert( bar.method2().equals("bar") );
assert( gee.method2().equals("bar") );

complete();

