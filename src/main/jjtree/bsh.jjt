/*****************************************************************************
 * Licensed to the Apache Software Foundation (ASF) under one                *
 * or more contributor license agreements.  See the NOTICE file              *
 * distributed with this work for additional information                     *
 * regarding copyright ownership.  The ASF licenses this file                *
 * to you under the Apache License, Version 2.0 (the                         *
 * "License"); you may not use this file except in compliance                *
 * with the License.  You may obtain a copy of the License at                *
 *                                                                           *
 *     http://www.apache.org/licenses/LICENSE-2.0                            *
 *                                                                           *
 * Unless required by applicable law or agreed to in writing,                *
 * software distributed under the License is distributed on an               *
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY                    *
 * KIND, either express or implied.  See the License for the                 *
 * specific language governing permissions and limitations                   *
 * under the License.                                                        *
 *                                                                           *
 *                                                                           *
 * This file is part of the BeanShell Java Scripting distribution.           *
 * Documentation and updates may be found at http://www.beanshell.org/       *
 * Patrick Niemeyer (pat@pat.net)                                            *
 * Author of Learning Java, O'Reilly & Associates                            *
 *                                                                           *
 *****************************************************************************/

/*
    Notes:
    There is probably a lot of room for improvement in here.
    All of the syntactic lookaheads have been commented with:
    These are probably expensive and we may want to start weeding them out
    where possible.
*/

options {
    JAVA_UNICODE_ESCAPE=true;
    JDK_VERSION="1.8";

    MULTI=true;
    NODE_DEFAULT_VOID=true;
    NODE_SCOPE_HOOK=true;
    NODE_PREFIX="BSH";
    NODE_PACKAGE="bsh";
    NODE_CLASS="bsh.SimpleNode";

    /* Unused options
    VISITOR=false;
    VISITOR_DATA_TYPE="Object";
    VISITOR_RETURN_TYPE="Object";
    VISITOR_EXCEPTION="";
    TRACK_TOKENS=false;
    NODE_USES_PARSER=false;
    */

    /* Print grammar debugging info as we parse
    DEBUG_PARSER=true;
    */
    /* Print detailed lookahead debugging info
    DEBUG_LOOKAHEAD=true;
    */

    /*
        There are weird issues related to this flag.
        The default value, true, creates line level error detail in the parse
        exceptions.  However it gives us strange LookaheadSuccess Errors thrown
        on some syntax errors.
        http://groups-beta.google.com/group/comp.compilers.tools.javacc/browse_thread/thread/14d3471883f8794f/ecf7b1d81151839c?q=Parser$LookaheadSuccess&rnum=1&hl=en#ecf7b1d81151839c
        I have manually hacked the Parser.java to catch LookaheadSuccess in
        the jj_rescan_token() method...  The bug report indicates that this
        is fixed, apparently in some post javacc 3.2 version.
    */
    //ERROR_REPORTING=false;

    // This breaks something for interactive use on the command line,
    // but may be useful in non-interactive use.
    //CACHE_TOKENS=true;
}

PARSER_BEGIN(Parser)
/*****************************************************************************
 * Licensed to the Apache Software Foundation (ASF) under one                *
 * or more contributor license agreements.  See the NOTICE file              *
 * distributed with this work for additional information                     *
 * regarding copyright ownership.  The ASF licenses this file                *
 * to you under the Apache License, Version 2.0 (the                         *
 * "License"); you may not use this file except in compliance                *
 * with the License.  You may obtain a copy of the License at                *
 *                                                                           *
 *     http://www.apache.org/licenses/LICENSE-2.0                            *
 *                                                                           *
 * Unless required by applicable law or agreed to in writing,                *
 * software distributed under the License is distributed on an               *
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY                    *
 * KIND, either express or implied.  See the License for the                 *
 * specific language governing permissions and limitations                   *
 * under the License.                                                        *
 *                                                                           *
 *                                                                           *
 * This file is part of the BeanShell Java Scripting distribution.           *
 * Documentation and updates may be found at http://www.beanshell.org/       *
 * Patrick Niemeyer (pat@pat.net)                                            *
 * Author of Learning Java, O'Reilly & Associates                            *
 *                                                                           *
 *****************************************************************************/
package bsh;

import java.io.IOException;
import java.io.Reader;

/**
    This is the BeanShell parser.  It is used internally by the Interpreter
    class (which is probably what you are looking for).  The parser knows
    only how to parse the structure of the language, it does not understand
    names, commands, etc.
    <p>
    You can use the Parser from the command line to do basic structural
    validation of BeanShell files without actually executing them. e.g.
    <code><pre>
        java bsh.Parser [ -p ] file [ file ] [ ... ]
    </pre></code>
    <p>
    The -p option causes the abstract syntax to be printed.
    <p>

    From code you'd use the Parser like this:
    <p
    <code><pre>
        Parser parser = new Parser(in);
        while( !(eof=parser.Line()) ) {
            SimpleNode node = parser.popNode();
            // use the node, etc. (See bsh.BSH* classes)
        }
    </pre></code>
*/
public class Parser
{
    boolean retainComments = false;

    public void setRetainComments( boolean b ) {
        retainComments = b;
    }

    void jjtreeOpenNodeScope(Node n) {
        ((SimpleNode)n).firstToken = getToken(1);
    }

    void jjtreeCloseNodeScope(Node n) {
        ((SimpleNode)n).lastToken = getToken(0);
    }

    /**
        Re-initialize the input stream and token source.
    */
    void reInitInput( Reader in ) {
        ReInit(in);
    }

    public SimpleNode popNode()
    {
        if ( jjtree.nodeArity() > 0)  // number of child nodes
            return (SimpleNode)jjtree.popNode();
        else
            return null;
    }

    /**
        Explicitly re-initialize just the token reader.
        This seems to be necessary to avoid certain looping errors when
        reading bogus input.  See Interpreter.
    */
    void reInitTokenInput( Reader in ) {
        jj_input_stream.reInit( in,
            jj_input_stream.getEndLine(),
            jj_input_stream.getEndColumn() );
    }

    public static void main( String [] args )
        throws IOException, ParseException
    {
        boolean print = false;
        int i=0;
        if ( args[0].equals("-p") ) {
            i++;
            print=true;
        }
        for(; i< args.length; i++) {
            Reader in = new FileReader(args[i]);
            Parser parser = new Parser(in);
            parser.setRetainComments(true);
            while( !parser.Line()/*eof*/ )
                if ( print )
                    System.out.println( parser.popNode() );
            in.close();
        }
    }

    /**
        Lookahead for the enhanced for statement.
        Expect "for" "(" and then see whether we hit ":" or a ";" first.
    */
    boolean isRegularForStatement()
    {
        int curTok = 1;
        Token tok = getToken(curTok++);
        if ( tok.kind != FOR ) return false;
        tok = getToken(curTok++);
        if ( tok.kind != LPAREN ) return false;
        while (true)
        {
            tok = getToken(curTok++);
            switch (tok.kind) {
                case COLON:
                    return false;
                case SEMICOLON:
                    return true;
                case EOF:
                    return false;
            }
        }
    }

    /**
        Lookahead for trailing dimensions on formal paramater.
        Expect "[]" and then see whether we hit "," or a ")" first.
        Abort on "=" or ";" which will indicate assignment.
    */
    boolean isFormalParameterDimensions()
    {
        int curTok = 1;
        Token tok = getToken(curTok++);
        if ( tok.kind != LBRACKET ) return false;
        tok = getToken(curTok++);
        if ( tok.kind != RBRACKET ) return false;
        while (true)
        {
            tok = getToken(curTok++);
            switch (tok.kind) {
                case LBRACKET:
                case RBRACKET:
                    continue;
                case RPAREN:
                case COMMA:
                    return true;
                case ASSIGN:
                case SEMICOLON:
                case LBRACE:
                case EOF:
                    return false;
            }
        }
    }

    /**
        Generate a ParseException with the specified message, pointing to the
        current token.
        The auto-generated Parser.generateParseException() method does not
        provide line number info, therefore we do this.
    */
    ParseException createParseException( String message, Exception e )
    {
        Token errortok = token;
        int line = errortok.beginLine, column = errortok.beginColumn;
        String mess = (errortok.kind == 0) ? tokenImage[0] : errortok.image;
        return new ParseException( "Parse error at line " + line
            + ", column " + column + " : " + message, e );
    }

    private int getRadix(String s) {
        if( s.startsWith("0b") || s.startsWith("0B") )
            return 2;
        if( s.startsWith("0x") || s.startsWith("0X") )
            return 16;
        if( s.startsWith("0") && s.length() > 1 )
            return 8;
        return 10;
    }

    private int getStartIdx(int radix) {
        return radix == 10 ? 0 : radix == 8 ? 1 : 2;
    }

    Primitive parseIntegral(String s) throws NumberFormatException {
        int radix = getRadix(s);
        return new Primitive(new java.math.BigInteger(s.substring(getStartIdx(radix)), radix));
    }
}

PARSER_END(Parser)

SKIP : /* WHITE SPACE and TYPE PARAMATERS */
{
    " " | "\t" | "\r" | "\f"
    | "\n"
    | < NONPRINTABLE: (["\u0000"-"\u0020", "\u0080"-"\u00ff"])+ >
    | < TYPE_PARAMATERS: "<" ( (~[">",";","|","&","\n","\r","="])* ("& "|">,")? )* ([">"])+ >
}

SPECIAL_TOKEN : /* COMMENTS */
{
/*
    SINGLE_LINE_COMMENT includes a hack to accept SLC at the end of a file
    with no terminanting linefeed.  This is actually illegal according to
    spec, but comes up often enough to warrant it... (especially in eval()).
*/
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")? >

| <HASH_BANG_COMMENT: "#!" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>

 /* Moved FORMAL_COMMENT to a real token.  Modified MULTI_LINE_COMMENT to not
    catch formal comments (require no star after star) */
| <MULTI_LINE_COMMENT:
    ("/***" (["*"])* | "/*") (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN : /* RESERVED WORDS AND LITERALS */
{
< ABSTRACT : "abstract" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < CLASS: "class" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package" >
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < STRICTFP : "strictfp" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < TRANSIENT: "transient" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["o","O","s","S","i","I","l","L","w","W"])?
      | <HEX_LITERAL> (["o","O","s","S","i","I","l","L","w","W"])?
      | <BINARY_LITERAL> (["o","O","s","S","i","I","l","L","w","W"])?
      | <OCTAL_LITERAL> (["o","O","s","S","i","I","l","L","w","W"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9","_"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F","_"])+ >
|
  < #BINARY_LITERAL: "0" ["b","B"] ((["0","1","_"])*(["0","1"])?) >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D","w","W"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D","w","W"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D","w","W"])?
      | (["0"-"9"])+ ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < #ESCAPE: "\\" (~["\\"]|["\\"]) >
|
  < CHARACTER_LITERAL: "'" (~["'","\n","\r","\\"] | <ESCAPE>)* "'" >
|
  < STRING_LITERAL: "\"" (~["\"","\n","\r","\\"] | <ESCAPE>)* "\"" >
|
  < LONG_STRING_LITERAL: "\"\"\"" ( ~["\""] ("\"")? )* "\"\"\"" >
|
   < FORMAL_COMMENT:
        "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/"
    >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

TOKEN : /* SEPARATORS */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

TOKEN : /* OPERATORS */
{
  < ASSIGN: "=" >
| < GT: ">" >
| < GTX: "@gt" >
| < LT: "<" >
| < LTX: "@lt" >
| < BANG: "!" >
| < TILDE: "~" >
| < EQ: "==" >
| < LE: "<=" >
| < LEX: "@lteq" >
| < GE: ">=" >
| < GEX: "@gteq" >
| < NE: "!=" >
| < BOOL_OR: "||" >
| < BOOL_ORX: "@or" >
| < BOOL_AND: "&&" >
| < BOOL_ANDX: "@and" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_ANDX: "@bitwise_and" >
| < BIT_OR: "|" >
| < BIT_ORX: "@bitwise_or" >
| < XOR: "^" >
| < XORX: "@bitwise_xor" >
| < MOD: "%" >
| < MODX: "@mod" >
| < POWER: "**" >
| < POWERX: "@pow" >
| < LSHIFT: "<<" >
| < LSHIFTX: "@left_shift" >
| < RSIGNEDSHIFT: ">>" >
| < RSIGNEDSHIFTX: "@right_shift" >
| < RUNSIGNEDSHIFT: ">>>" >
| < RUNSIGNEDSHIFTX: "@right_unsigned_shift" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ANDASSIGNX: "@and_assign" >
| < ORASSIGN: "|=" >
| < ORASSIGNX: "@or_assign" >
| < XORASSIGN: "^=" >
| < XORASSIGNX: "@xor_assign" >
| < MODASSIGN: "%=" >
| < MODASSIGNX: "@mod_assign" >
| < POWERASSIGN: "**=" >
| < POWERASSIGNX: "@pow_assign" >
| < LSHIFTASSIGN: "<<=" >
| < LSHIFTASSIGNX: "@left_shift_assign" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RSIGNEDSHIFTASSIGNX: "@right_shift_assign" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < RUNSIGNEDSHIFTASSIGNX: "@right_unsigned_shift_assign" >
| < ARROW: "->" >
| < SPACESHIP: "<=>" >
| < NULLCOALESCEASSIGN: "??=" >
| < NULLCOALESCE: "??" >
| < ELVIS: "?:" >
| < HOOK: "?" >
| < COLON: ":" >
| < ELLIPSIS: "..." >
}


/*
    Thanks to Sreenivasa Viswanadha for suggesting how to get rid of expensive
    lookahead here.
*/
boolean Line() :
{}
{
  <EOF> {
    Interpreter.debug("End of File!");
    return true;
  }
|
  BlockStatement() {
    return false;
  }
}

/*****************************************
 * THE JAVA LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

/*
    Gather modifiers for a class, method, or field.
    I lookahead is true then we are being called as part of a lookahead and we
    should not enforce any rules.  Otherwise we validate based on context
    (field, method, class)
*/
Modifiers Modifiers( int context, boolean lookahead ) :
{
    Modifiers mods = null;
}
{
  (
    (
        "private" | "protected" | "public" | "synchronized" | "final"
        | "native" | "transient" | "volatile" | "abstract" | "static"
        | "strictfp" | "default"
    ) {
        if ( !lookahead )
            try {
                if ( mods == null ) mods = new Modifiers( context );
                mods.addModifier( getToken(0).image );
            } catch ( IllegalStateException e ) {
                throw createParseException( e.getMessage(), e );
            }
    }
  )* {
    return mods;
  }
}


/**
*/
void ClassDeclaration() #ClassDeclaration :
{
    Modifiers mods;
    Token name;
    int numInterfaces;
}
{
    mods = Modifiers( Modifiers.CLASS, false )
  (
    (
        "class" { jjtThis.type = ClassGenerator.Type.CLASS; } name=<IDENTIFIER>
        [ "extends" AmbiguousName() { jjtThis.extend = true; } ]
        [ "implements" numInterfaces=NameList()
        { jjtThis.numInterfaces=numInterfaces; } ]
      |
        "interface" { jjtThis.type = ClassGenerator.Type.INTERFACE; } name=<IDENTIFIER>
        [ "extends" numInterfaces=NameList()
            { jjtThis.numInterfaces=numInterfaces; } ]
    )
    Block()
  |
    "enum" { jjtThis.type = ClassGenerator.Type.ENUM; } name=<IDENTIFIER>
    [ "implements" numInterfaces=NameList()
    { jjtThis.numInterfaces=numInterfaces; } ]
    EnumBlock()
  )
    {
        if (null != mods) jjtThis.modifiers = mods;
        jjtThis.name = name.image;
    }
}

void EnumConstant() #EnumConstant:
{
    Token t;
}
{
  t=<IDENTIFIER> [ Arguments() ] [ Block() ]
  {
    jjtThis.name=t.image;
  }
}

void EnumBlock() #Block :
{
    Token s = null;
}
{
    "{" ( EnumConstant() )? ( "," EnumConstant() )* [ ";" ( BlockStatement() )* ] "}"
}

void MethodDeclaration() #MethodDeclaration :
{
    Token t = null;
    Modifiers mods;
    int count;
}
{
    mods = Modifiers( Modifiers.METHOD, false ) { if (null != mods) jjtThis.modifiers = mods; }
(
    LOOKAHEAD( <IDENTIFIER> "(" )
    t = <IDENTIFIER> { jjtThis.name = t.image; }
|
    ReturnType()
    t = <IDENTIFIER> { jjtThis.name = t.image; }
)
    FormalParameters()
    [ "throws" count=NameList() { jjtThis.numThrows=count; } ]
    ( Block() | ";" )
}

void PackageDeclaration () #PackageDeclaration:
{ }
{
    "package" AmbiguousName()
}

void ImportDeclaration() #ImportDeclaration :
{ }
{
  LOOKAHEAD( 3 )
  "import" [ "static" { jjtThis.staticImport = true; } ]
  AmbiguousName() [ "." "*" { jjtThis.importPackage = true; } ] ";"
|
   // bsh super import statement
  "import" "*" ";" { jjtThis.superImport = true; }
}

void VariableDeclarator() #VariableDeclarator :
{
    Token t;
}
{
    t=<IDENTIFIER>
    ( "[" "]" { jjtThis.dimensions++; } )*
    [ "=" VariableInitializer() ]
    { jjtThis.name = t.image; }
}

void VariableInitializer() :
{}
{
  LOOKAHEAD(3)
  ArrayInitializer()
|
  Expression()
}

void ArrayInitializer() #ArrayInitializer :
{}
{
  "{" [ VariableInitializer()
        ( LOOKAHEAD(2) "," VariableInitializer() )* ] [ "," ] "}"
}

void FormalParameters() #FormalParameters :
{}
{
  "(" [ FormalParameter() ( "," FormalParameter() )* ] ")"
}

void FormalParameter() #FormalParameter :
{
  Token t;
}
{
( LOOKAHEAD(2) [ "final" { jjtThis.isFinal = true; } ]
  Type()[ <ELLIPSIS> { jjtThis.isVarArgs = true; } ]
  t=<IDENTIFIER> { jjtThis.name = t.image; }
|
  t=<IDENTIFIER> { jjtThis.name = t.image; }
)
  ( LOOKAHEAD({ isFormalParameterDimensions() }) "[" "]" { jjtThis.dimensions++; } )*
}


/*
    Type, name and expression syntax follows.
*/
void Type() #Type :
{ }
{
    /*
        The embedded lookahead is (was?) necessary to disambiguate for
        PrimaryPrefix.  ( )* is a choice point.  It took me a while to
        figure out where to put that.  This stuff is annoying.
    */
  ( PrimitiveType() | AmbiguousName() )
    ( LOOKAHEAD(2) "[" "]" { jjtThis.addArrayDimension(); } )*
}

/*
    Originally called ResultType in the grammar
*/
void ReturnType() #ReturnType :
{ }
{
  "void" { jjtThis.isVoid = true; }
|
  Type()
}

void PrimitiveType() #PrimitiveType :
{ } {
  "boolean" { jjtThis.type = Boolean.TYPE; }
| "char" { jjtThis.type =  Character.TYPE; }
| "byte" { jjtThis.type =  Byte.TYPE; }
| "short" { jjtThis.type =  Short.TYPE; }
| "int" { jjtThis.type =  Integer.TYPE; }
| "long" { jjtThis.type =  Long.TYPE; }
| "float" { jjtThis.type =  Float.TYPE; }
| "double" { jjtThis.type =  Double.TYPE; }
}

void AmbiguousName() #AmbiguousName :
/*
    A lookahead of 2 is required below since "Name" can be followed by a ".*"
    when used in the context of an "ImportDeclaration".
*/
{
    Token t;
    StringBuilder s;
}
{
  t = <IDENTIFIER> {
    s = new StringBuilder(t.image);
  }
  ( LOOKAHEAD(2) "." t = <IDENTIFIER> { s.append("."+t.image); } )* {
    jjtThis.text = s.toString();
  }
}

int NameList() :
{ int count = 1; }
{
  AmbiguousName() ( "," AmbiguousName() { ++count; } )*
  { return count; }
}

/*
 * This expansion has been written this way instead of:
 *   Assignment() | ConditionalExpression()
 * for performance reasons.
 */
void Expression() #Assignment :
{ int op ; }
{
  ConditionalExpression()
  [ op=AssignmentOperator() { jjtThis.operator = op; } Expression() ]
}

int AssignmentOperator() :
{ Token t; }
{
    ( "=" | "*=" | "**=" | "@pow_assign" | "/=" | "%=" | "@mod_assign" | "+="
        | "-=" | "&=" | "@and_assign" | "^=" | "@xor_assign" | "|="
        | "@or_assign" | "<<=" | "@left_shift_assign" | ">>=" | "??="
        | "@right_shift_assign" | ">>>=" | "@right_unsigned_shift_assign" )
    {
        t = getToken(0);
        return t.kind;
    }
}

void ConditionalExpression() :
{ }
{
  NullCoalesceElvisSpaceShipExpression() [ <HOOK> Expression() <COLON> ConditionalExpression()
    #TernaryExpression(3) ]
}

void NullCoalesceElvisSpaceShipExpression() :
{ Token t=null; }
{
  ConditionalOrExpression()
  ( ( t = <NULLCOALESCE> | t = <ELVIS> | t = <SPACESHIP> )
    ConditionalOrExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void ConditionalOrExpression() :
{ Token t=null; }
{
  ConditionalAndExpression()
  ( ( t = <BOOL_OR> | t = <BOOL_ORX> )
    ConditionalAndExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void ConditionalAndExpression() :
{ Token t=null; }
{
  InclusiveOrExpression()
  ( ( t = <BOOL_AND> | t = <BOOL_ANDX> )
    InclusiveOrExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void InclusiveOrExpression() :
{ Token t=null; }
{
  ExclusiveOrExpression()
  ( ( t = <BIT_OR> | t = <BIT_ORX> )
    ExclusiveOrExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void ExclusiveOrExpression() :
{ Token t=null; }
{
   AndExpression()
      ( ( t = <XOR> | t = <XORX> )
        AndExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void AndExpression() :
{ Token t=null; }
{
   EqualityExpression()
  (( t = <BIT_AND> | t = <BIT_ANDX> )
    EqualityExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void EqualityExpression() :
{ Token t = null; }
{
  InstanceOfExpression()
     (( t = <EQ> | t= <NE> )
      InstanceOfExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2)
  )*
}

void InstanceOfExpression() :
{ Token t = null; }
{
  RelationalExpression()
  [t = <INSTANCEOF> Type() { jjtThis.kind = t.kind; } #BinaryExpression(2) ]
}

void RelationalExpression() :
{ Token t = null; }
{
  ShiftExpression()
  (( t = <LT> | t = <LTX> | t = <GT> | t = <GTX> |
      t = <LE> | t = <LEX> | t = <GE> | t = <GEX> )
  ShiftExpression()
  { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void ShiftExpression() :
{ Token t = null; }
{
  AdditiveExpression()
  (( t = <LSHIFT> | t = <LSHIFTX> | t = <RSIGNEDSHIFT> |
      t = <RSIGNEDSHIFTX> | t = <RUNSIGNEDSHIFT> | t = <RUNSIGNEDSHIFTX> )
  AdditiveExpression()
  { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void AdditiveExpression() :
{ Token t = null; }
{
  MultiplicativeExpression()
   ( ( t = <PLUS> | t = <MINUS> )
  MultiplicativeExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2)  )*
}

void MultiplicativeExpression() :
{ Token t = null; }
{
  PowerExpression()
     (( t = <STAR> | t = <SLASH> | t = <MOD> | t = <MODX> )
      PowerExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void PowerExpression() :
{ Token t = null; }
{
  UnaryExpression()
     (( t = <POWER> | t = <POWERX> )
      UnaryExpression()
    { jjtThis.kind = t.kind; } #BinaryExpression(2) )*
}

void UnaryExpression() :
{ Token t = null; }
{
   // prefix sign
  ( t = <PLUS> | t = <MINUS> | t = <TILDE> | t = <BANG> ) UnaryExpression()
    { jjtThis.kind = t.kind; } #UnaryExpression(1)
|
  // prefix incr/decr
  (t = <INCR> | t = <DECR>) PrimaryExpression()
    { jjtThis.kind = t.kind; } #UnaryExpression(1)
|
  // cast
  LOOKAHEAD( CastLookahead() ) CastExpression()
|
  // postfix incr/decr
  PrimaryExpression() [ (t = <INCR> | t = <DECR>)
    { jjtThis.kind = t.kind; jjtThis.postfix = true; } #UnaryExpression(1) ]
}

// This production only looks ahead to see if the next matches a cast.
void CastLookahead() : { }
{
  LOOKAHEAD(2) <LPAREN> PrimitiveType()
|
  LOOKAHEAD( <LPAREN> AmbiguousName() <LBRACKET> ) <LPAREN> AmbiguousName() <LBRACKET> <RBRACKET>
|
 <LPAREN> AmbiguousName() <RPAREN> ( <TILDE> | <BANG> | <LPAREN> | <IDENTIFIER> | <NEW> | Literal() | ArrayDimensions() )
}

void CastExpression() #CastExpression :
{ }
{
  "(" Type() ")" UnaryExpression()
}

void PrimaryExpression() #PrimaryExpression : { }
{
  PrimaryPrefix() ( PrimarySuffix() )*
}

void MethodInvocation() #MethodInvocation : { }
{
   AmbiguousName() Arguments()
}

void PrimaryPrefix() : { }
{
  Literal()
|
  "(" Expression() ")"
|
  AllocationExpression()
|
  LOOKAHEAD( AmbiguousName() "(" ( Expression() | ")" ) )
    MethodInvocation()
|
  LOOKAHEAD( Type() "." "class" )
    Type()
|
  AmbiguousName()

/*
|
  LOOKAHEAD( "void" "." "class" )
*/
}

void PrimarySuffix() #PrimarySuffix :
{
    Token t = null;
}
{
  LOOKAHEAD(2)
  "." "class" {
        jjtThis.operation = BSHPrimarySuffix.CLASS;
    }
|
  "[" [ Expression() { jjtThis.hasLeftIndex = true; } ]
     ( ":" { jjtThis.slice = true; }
      [ Expression() { jjtThis.hasRightIndex = true; } ]
     ( ":" { jjtThis.step = true; } [ Expression() ] )? )? "]" {
        jjtThis.operation = BSHPrimarySuffix.INDEX;
    }
|
    // Safe navigate field access or method invocation
  LOOKAHEAD(2)
  "?." t = <IDENTIFIER> [ Arguments() ] {
        jjtThis.operation = BSHPrimarySuffix.NAME;
        jjtThis.field = t.image;
        jjtThis.safeNavigate = true;
    }
|
    // Field access or method invocation
  LOOKAHEAD(2)
  "." t = <IDENTIFIER> [ Arguments() ] {
        jjtThis.operation = BSHPrimarySuffix.NAME;
        jjtThis.field = t.image;
    }
|
  "{" Expression() "}" {
        jjtThis.operation = BSHPrimarySuffix.PROPERTY;
    }
/*  For inner classes */
|
  LOOKAHEAD(2)
  "." AllocationExpression() {
        jjtThis.operation = BSHPrimarySuffix.NEW;
    }
}

void Literal() #Literal :
{
    Token x;
    boolean b;
    String literal;
    char ch;
    int last;
}
{
  x = <INTEGER_LITERAL>
  {
    literal = x.image.replaceAll("_", "");
    last = literal.length() - 1;
    ch = literal.charAt(last);
    try {
        if( Types.Suffix.isIntegral(ch) )
            jjtThis.value = parseIntegral(literal.substring(0, last))
                  .castToType(Types.Suffix.getIntegralType(ch), 0);
        else
            jjtThis.value = Primitive.shrinkWrap(parseIntegral(literal).getValue());
    } catch ( NumberFormatException e ) {
        throw createParseException( e.getMessage(), e );
    }
  }
|
  x = <FLOATING_POINT_LITERAL>
  {
    literal = x.image;
    last = literal.length() - 1;
    ch = literal.charAt(last);
    try {
        if( Types.Suffix.isFloatingPoint(ch) )
            jjtThis.value = new Primitive(new java.math.BigDecimal(literal.substring(0, last)))
                   .castToType(Types.Suffix.getFloatingPointType(ch), 0);
        else
            jjtThis.value = Primitive.shrinkWrap( new java.math.BigDecimal( literal ) );
    } catch ( NumberFormatException e ) {
        throw createParseException( e.getMessage(), e );
    }
  }
|
  x = <CHARACTER_LITERAL> {
        try {
            jjtThis.charSetup( x.image.substring(1, x.image.length() - 1) );
        } catch ( Exception e ) {
            throw createParseException("Error parsing character: "+x.image, e);
        }
    }
|
  x = <STRING_LITERAL> {
        try {
            jjtThis.stringSetup( x.image.substring(1, x.image.length() - 1) );
        } catch ( Exception e ) {
            throw createParseException("Error parsing string: "+x.image, e);
        }
    }
|
  x = <LONG_STRING_LITERAL> {
        try {
            jjtThis.value = x.image.substring(3, x.image.length() - 3);
        } catch ( Exception e ) {
            throw createParseException("Error parsing long string: "+x.image, e);
        }
    }
|
  b = BooleanLiteral()  {
    jjtThis.value = b ? Primitive.TRUE : Primitive.FALSE; }
|
  NullLiteral() {
    jjtThis.value = Primitive.NULL;
}
|
 VoidLiteral() {
    jjtThis.value = Primitive.VOID; }
}

boolean BooleanLiteral() :
{}
{
  "true" { return true; }
|
  "false" { return false; }
}

void NullLiteral() :
{}
{
  "null"
}

void VoidLiteral() :
{}
{
  "void"
}

void Arguments() #Arguments :
{ }
{
  "(" [ ArgumentList()  ]  ")"
}

// leave these on the stack for Arguments() to handle
void ArgumentList() :
{ }
{
  Expression()
  ( "," Expression() )*
}

void AllocationExpression() #AllocationExpression :
{ }
{
  "new" (
      PrimitiveType() ArrayDimensions()
   |
      AmbiguousName() (
        ArrayDimensions()
      |
        Arguments() [ LOOKAHEAD(2) Block() ]
      )
   |
      ArrayDimensions()
   )
 |
   ArrayDimensions()
}

void ArrayDimensions() #ArrayDimensions :
{}
{
    // e.g. int [4][3][][];
  LOOKAHEAD(2)
  ( LOOKAHEAD(2) "[" Expression() "]" { jjtThis.addDefinedDimension(); } )+
  ( LOOKAHEAD(2) "[" "]" { jjtThis.addUndefinedDimension(); } )*
|
    // e.g. int [][] { {1,2}, {3,4} };
  ( "[" "]" { jjtThis.addUndefinedDimension(); } )+ ArrayInitializer()
|
    // e.g. { {1,2}, {3,4} };
  ArrayInitializer() { jjtThis.numUndefinedDims = -1; }
}


/*
 * Statement syntax follows.
 */

void Statement() : { }
{
  LOOKAHEAD(2)
  LabeledStatement()
|
  LOOKAHEAD(3)
  Block()
|
  EmptyStatement()
|
  StatementExpression() ";"
|
  SwitchStatement()
|
  IfStatement()
|
  WhileStatement()
|
  DoStatement()
|
  LOOKAHEAD ( { isRegularForStatement() } )
  ForStatement()
|
  EnhancedForStatement()
|
  BreakStatement()
|
  ContinueStatement()
|
  ReturnStatement()
|
  SynchronizedStatement()
|
  ThrowStatement()
|
  TryStatement()
}

void LabeledStatement() #LabeledStatement :
{ Token t = null; }
{
  t=<IDENTIFIER> ":" Statement()
  { jjtThis.label=t.image; }
}

void Block() #Block :
{
    Token s = null;
}
{
/*
    Doing "static" blocks here means that we'll have to throw runtime
    exceptions if it's not used in an appropriate place.
    Is it worth moving the grammar around to make this a static parse time
    check?  (Sreeni? ;) )
*/
    [ s = "static" ] "{" ( BlockStatement() )* "}"
    {
        if ( s != null ) jjtThis.isStatic = true;
    }
}

void BlockStatement() :
{
}
{
  LOOKAHEAD( Modifiers( Modifiers.FIELD, true ) ( "class" | "interface" | "enum" ) )
  ClassDeclaration()
|
  LOOKAHEAD ( Modifiers( Modifiers.METHOD, true )
        ReturnType() <IDENTIFIER> "("
  )
    MethodDeclaration()
|
  LOOKAHEAD ( Modifiers( Modifiers.METHOD, true )
        <IDENTIFIER> FormalParameters() [ "throws" NameList() ] "{"
  )
    MethodDeclaration()
|
  LOOKAHEAD( Modifiers( Modifiers.FIELD, true ) Type() <IDENTIFIER> )
  TypedVariableDeclaration() ";"
|
  Statement()
|
  // Allow BeanShell imports in any block
  ImportDeclaration()
|
  // Allow BeanShell package declarations in any block
  PackageDeclaration()
|
  FormalComment()
}

void FormalComment() #FormalComment( retainComments ) :
{
    Token t;
}
{
    t=<FORMAL_COMMENT>  {
        jjtThis.text=t.image;
    }
}

void EmptyStatement() :
{}
{
  ";"
}

void StatementExpression() :
{ }
{
  /*
    This is looser than normal Java to simplify the grammar.  This allows
    us to type arbitrary expressions on the command line, e.g. "1+1;"
    We should turn this off in the implementation in strict java mode.
  */
  Expression()
/*
    // This was the original Java grammar.

    //  Original comment:
    //  The last expansion of this production accepts more than the legal
    //  Java expansions for StatementExpression.
      PreIncrementExpression()
    |
      PreDecrementExpression()
    |
      LOOKAHEAD( PrimaryExpression() AssignmentOperator() )
      Assignment() { }
    |
      PostfixExpression()
*/

}

void SwitchStatement() #SwitchStatement :
{}
{
  "switch" "(" Expression() ")" "{"
    ( SwitchLabel() ( BlockStatement() )* )*
  "}"
}

void SwitchLabel() #SwitchLabel :
{}
{
  "case" Expression() ":"
|
  "default:" { jjtThis.isDefault = true; }
}

void IfStatement() #IfStatement :
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{}
{
  "if" "(" Expression() ")" Statement() [ LOOKAHEAD(1) "else" Statement() ]
}

void WhileStatement() #WhileStatement :
{}
{
  "while" "(" Expression() ")" Statement()
}

/*
    Do statement is just a While statement with a special hook to execute
    at least once.
*/
void DoStatement() #WhileStatement :
{}
{
  "do" Statement() "while" "(" Expression() ")" ";"
    { jjtThis.isDoStatement=true;  }
}

void ForStatement() #ForStatement :
{ Token t = null; }
{
  "for" "(" [ ForInit() { jjtThis.hasForInit=true; } ]
    ";" [ Expression() { jjtThis.hasExpression=true; } ]
    ";" [ ForUpdate() { jjtThis.hasForUpdate=true; } ] ")"
    Statement()
}

/*
    The new JDK1.5 enhanced for statement.
*/
void EnhancedForStatement() #EnhancedForStatement :
{
  Token t = null;
}
{
  LOOKAHEAD( 4 ) // look ahead for the ":" before deciding
  "for" "(" t=<IDENTIFIER> ":" Expression() ")"
  Statement() { jjtThis.varName = t.image; }
|
  "for" "(" [ "final" { jjtThis.isFinal = true; } ]
  Type() t=<IDENTIFIER> ":" Expression() ")"
  Statement() { jjtThis.varName = t.image; }
}

void ForInit() :
{ Token t = null; }
{
  LOOKAHEAD( Modifiers( Modifiers.FIELD, true ) Type() <IDENTIFIER> )
  TypedVariableDeclaration()
|
  StatementExpressionList()
}

/**
    Declared a typed variable.
    Untyped variables are not declared per-se but are handled by the part
    of the grammar that deals with assignments.
*/
void TypedVariableDeclaration() #TypedVariableDeclaration :
{
    Token t = null;
    Modifiers mods;
}
{
    mods = Modifiers( Modifiers.FIELD, false )
    Type() VariableDeclarator() ( "," VariableDeclarator() )*
    {
        if (null != mods) jjtThis.modifiers = mods;
    }
}

void StatementExpressionList() #StatementExpressionList :
{}
{
  StatementExpression() ( "," StatementExpression() )*
}

void ForUpdate() :
{}
{
  StatementExpressionList()
}

void BreakStatement() #ReturnStatement :
{ Token t = null; }
{
  "break" [ t=<IDENTIFIER> { jjtThis.label = t.image; } ] ";" { jjtThis.kind = BREAK; }
}

void ContinueStatement() #ReturnStatement :
{ Token t = null; }
{
  "continue" [ t=<IDENTIFIER> { jjtThis.label = t.image; } ] ";" { jjtThis.kind = CONTINUE; }
}

void ReturnStatement() #ReturnStatement :
{}
{
  "return" [ Expression() ] ";" { jjtThis.kind = RETURN; }
}

void SynchronizedStatement() #Block :
{
}
{
  "synchronized" "(" Expression() ")" Block() {
        jjtThis.isSynchronized=true;
  }
}

void ThrowStatement() #ThrowStatement :
{}
{
  "throw" Expression() ";"
}

void TryStatement() #TryStatement:
/*
    Semantic check required here to make sure that at least one
    finally/catch is present.  (You can have a try with finally and no catch).
 */
{ boolean closed = false; }
{
    "try" [ TryWithResources() ] Block()
    ( "catch" "(" MultiCatch() ")" Block() { closed = true; } )*
    [ "finally" Block() { closed = true; } ]
    {
        if ( !closed ) throw generateParseException();
    }
}

void MultiCatch() #MultiCatch :
{ Token t; }
{
  LOOKAHEAD(2) [ "final" { jjtThis.isFinal = true; } ]
  Type() ( "|" Type() )*
  t=<IDENTIFIER> { jjtThis.name = t.image; }
|
  t=<IDENTIFIER> { jjtThis.name = t.image; }
}

void TryWithResources() #TryWithResources :
{  }
{
   "(" AutoCloseable() ( ";" AutoCloseable() )* ")"
}

void AutoCloseable() #AutoCloseable :
{  }
{
  LOOKAHEAD(2)
       Type() VariableDeclarator()
    |
        VariableDeclarator()
}
