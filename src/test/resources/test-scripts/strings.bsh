#!/bin/java bsh.Interpreter

source("TestHarness.bsh");
source("Assert.bsh");
import bsh.BSHLiteral;

boolean currentInternStrings = BSHLiteral.internStrings;
BSHLiteral.internStrings = true;

// char is not a string
assert('a' == 'a');
assert('a' != "a");
assert("\u3456".charAt(0) == '\u3456');
assert('\101' == 'A');
assert('\101' != "\101");
assert('A' == 65);
assert(new Character('a') == 'a');
assert(new Character("a".charAt(0)) == 'a');
assert(Character.valueOf('a') == 'a');
assert(Character.valueOf("a".charAt(0)) == 'a');
assert(!"abc".substring(2).equals('c'));
assert('a' instanceof bsh.Primitive);
assert('a'.getType() == Character.TYPE);

// not a char is a string
assert('' instanceof String);
assert("" instanceof String);
assert("a" instanceof String);
assert('ab' instanceof String);
assert("a" == "a");
assert("\132" == "Z");
assert('\488' == "\00488"); // not a valid char octal
assert('' == '');
assert('' == "");
assert("" == "");
assert('ab' == 'ab');
assert('ab' == "ab");
assert("ab" == "ab");
assert('ab'.equals('ab'));
assert('ab'.equals("ab"));
assert("ab".equals("ab"));
assert("ab".equals('ab'));
assert('abc'.equals('abc'));
assert('abc'.equals("abc"));
assert("abc".equals("abc"));
assert("abc".equals('abc'));
assert("abc".charAt(0) == 'a');
assert("abc".charAt(0) != "a");
assert("abc".substring(1).equals('bc'));
assert("abc".substring(1).equals("bc"));
assert("abc".substring(2).equals("c"));
assert("\"quoted\"" == '"quoted"');
assert('\'quoted\'' == "'quoted'");
assert('xb'.concat('xb').equals('xbxb'));

assertThat("a\bcde\fg", containsString("cde"));
assertThat("abc\8def", equalTo("abc8def"));
assertThat("abc\78def", endsWith("8def"));
assertThat("abc\378def", endsWith("8def"));
BSHLiteral.internStrings = false;
assertThat("abc\3def", endsWith("def"));
BSHLiteral.internStrings = true;

// additional edge cases
assertEquals('\n', '\12');
assertEquals('\n', '\012');
assertEquals("\n", "\12");
assertEquals("\n", "\012");
assertEquals("?7", '\777');
assertEquals("?7", "\777");
assertEquals("?77", '\7777');
assertEquals("?77", "\7777");
assertEquals("Is a question?", "Is a question\77");

// escape tests
assertEquals("aFb", 'a\\b'.replace('\\','F'));
assertEquals("aFb", "a\\b".replace('\\','F'));
assertEquals("aFb", "a\\b".replace("\\","F"));

assertEquals("aFb", 'a\'b'.replace('\'','F'));
assertEquals("aFb", "a\'b".replace('\'','F'));
assertEquals("aFb", "a\'b".replace("\'","F"));

assertEquals("aFb", 'a\"b'.replace('\"','F'));
assertEquals("aFb", "a\"b".replace('\"','F'));
assertEquals("aFb", "a\"b".replace("\"","F"));

assertEquals("aFb", 'a\tb'.replace('\t','F'));
assertEquals("aFb", "a\tb".replace('\t','F'));
assertEquals("aFb", "a\tb".replace("\t","F"));

assertEquals("aFb", 'a\6b'.replace('\6','F'));
assertEquals("aFb", "a\6b".replace('\6','F'));
assertEquals("aFb", "a\6b".replace("\6","F"));

assertEquals("aFb", 'a\nb'.replace('\n','F'));
assertEquals("aFb", "a\nb".replace('\n','F'));
assertEquals("aFb", "a\nb".replace("\n","F"));

// escape like a boss
/* Doesn't work in windows it only escapes the CR leaving the LF
line = "abc\
def\
ghi\
";
assertEquals("abc\ndef\nghi\n", line);
line = 'abc\
def\
ghi\
';
assertEquals('abc\ndef\nghi\n', line);
*/

// Long strings
out= "\n";
for (x : 5)
  out += x + "\n";

aout = """
0
1
2
3
4
5
""";
assert(aout.replace("\r","").equals(out));

assert("""
This is a long string
containing "a quote" within
the string.
""".contains('"a quote"'));

// String concatenation
a="foo";
a+="bar";
a+=5;
a+=null;
a+="a";

assert( a.equals("foobar5nulla") );

a='foo';
a+='bar';
a+=5;
a+=null;
a+='a';

assert( a.equals('foobar5nulla') );

b="foo"+"bar"+5;
assert(b.equals('foobar5'));

// String repeat
assertEquals("", "aaa" * 0);
assertEquals("", "aaa" * !true);
assertEquals("", "aaa" * false);
assertEquals("", "aaa" * null);
assertEquals("", 0 * "aaa");
assertEquals("", !true * "aaa");
assertEquals("", false * "aaa");
assertEquals("", null * "aaa");
assertEquals("aaa", "aaa" * 1);
assertEquals("aaa", "aaa" * true);
assertEquals("aaa", "aaa" * !false);
assertEquals("aaa", 1 * "aaa");
assertEquals("aaa", true * "aaa");
assertEquals("aaa", !false * "aaa");

// String constants comparison (intern strings on) see #343
BSHLiteral.internStrings = true;
String s0 = "dcba";
String s1 = "dcb"+"a";
String s2 = new String("dcba");
String s3 = "dc";
s3 += "ba";
assertTrue("s0.equals(s1) is true", s0.equals(s1));
assertTrue("s0.equals(s2) is true", s0.equals(s2));
assertTrue("s0.equals(s3) is true", s0.equals(s3));
assertTrue("s0 == s1 is true", s0 == s1);
assertFalse("s0 == s2 is false", s0 == s2);
assertTrue("s0 == s3 is true", s0 == s3);
assertTrue("'h'+'gfe' == 'hg'+'fe' is true", 'h'+'gfe' == 'hg'+'fe');
assertTrue('"lkj"+"i" == "lk"+"ji" is true', "lkj"+"i" == "lk"+"ji");
assertTrue('"abc" * 3 == "abc"+"abc"+"abc"', "abc" * 3 == "abc"+"abc"+"abc");
assertTrue('3 * "de" == "dedede"', 3 * "de" == "dedede");

// without string constant comparison (intern strings off)
BSHLiteral.internStrings = false;
String s0 = "ponm";
String s1 = "pon"+"m";
String s2 = new String("ponm");
String s3 = "po";
s3 += "nm";
assertTrue("s0.equals(s1) is true", s0.equals(s1));
assertTrue("s0.equals(s2) is true", s0.equals(s2));
assertTrue("s0.equals(s3) is true", s0.equals(s3));
assertFalse("s0 == s1 is false", s0 == s1);
assertFalse("s0 == s2 is false", s0 == s2);
assertFalse("s0 == s3 is false", s0 == s3);
assertFalse("'t'+'srq' == 'ts'+'rq' is false", 't'+'srq' == 'ts'+'rq');
assertFalse('"xwv"+"u" == "xw"+"vu" is false', "xwv"+"u" == "xw"+"vu");
assertFalse('"fgh" * 3 == "fgh"+"fgh"+"fgh"', "fgh" * 3 == "fgh"+"fgh"+"fgh");
assertFalse('3 * "kl" == "klklkl"', 3 * "kl" == "klklkl");
BSHLiteral.internStrings = true;

// reset intern strings flag the way we found it
BSHLiteral.internStrings = currentInternStrings;

complete();
